#!/usr/bin/perl
# Abhijit Menon-Sen <ams@toroid.org>
#

use IO::Select;
use IO::Socket::SSL;

$verbose = 0;
while ( $ARGV[0] eq '-v' ) {
    shift @ARGV;
    $verbose++;
}

$pw = $ENV{GMAIL_PASSWORD};
$user = shift @ARGV || die "Syntax: gmail-fetch [-v] <username> <mailbox> ...";
die "Syntax: gmail-fetch [-v] <username> <mailbox> ...\n" unless @ARGV;

mkdir "gmail" unless -d "gmail";

foreach my $mailbox ( @ARGV ) {
    ( my $local = $mailbox ) =~ y{/ }{._};
    mkdir "gmail/$local" unless -d "gmail/$local";

    do {
        eval { fetch( $mailbox, $local ) };
        print "ERROR: $@" and sleep 2 if $@;
    } while ( $@ );
}

sub fetch {
    my ( $m, $l ) = @_;

    my $buf;
    my $state = 0;
    my $literal = 0;
    my ( $msns, @msns, @tmps );
    my ( $dir, $file, $fh, $uid, $uidvalidity );

    print "Connecting to imap.gmail.com:993\n";
    my $imap = IO::Socket::SSL->new( "imap.gmail.com:993" )
        or die "Couldn't connect: " . IO::Socket::SSL::errstr();

    unless ( defined $imap->blocking( 0 ) ) {
        die "Couldn't set socket non-blocking: $!";
    }

    my $sel = IO::Select->new( $imap );
    while ( 1 ) {
        next unless $sel->can_read( 2 );

        my $n = $imap->sysread( $buf, 1024, length $buf );

        if ( not defined $n ) {
            # A read error here would normally mean something broke, but
            # with IO::Socket::SSL, it seems to mean "no data available
            # right now".
            next;
        }
        elsif ( $n == 0 ) {
            die "Connection closed unexpectedly in state $state"
                unless $state eq "logout";
            return;
        }

        # If there's any more data available, get it all.
        while ( defined $n && $n == 1024 ) {
            $n = $imap->sysread( $buf, 1024, length $buf )
        }

        # If we're reading a literal, we'll avoid the usual line-by-line
        # processing and go straight to the literal-reading code inside.
        goto LITERAL if $literal;

        while ( $buf =~ s/^(.*?)\r\n//sm ) {
            my $line = $1;

            if ( $verbose ) {
                if ( $line =~ /^\* \d+ FETCH \(UID \d+\)$/ ) {
                    $msns++;
                    if ( $verbose > 1 ) {
                        print "S: $line\n";
                    }
                    elsif ( $msns == 1 ) {
                        print "S: <...MSN-UID map...>\n";
                    }
                }
                else {
                    print "S: $line\n";
                    $msns = 0;
                }
            }

            if ( $line =~ /^x(?:yz)? BAD /i ) {
                print "ERROR: Unexpected syntax error\n";
                return;
            }

            if ( $state == 0 ) {
                unless ( $pw ) {
                    system( "stty -echo" );
                    print "Password: ";
                    chomp( $pw = <STDIN> );
                    print "\n";
                    system( "stty echo" );
                }
                $imap->writeln( qq{x LOGIN "$user" "$pw"} );
                $state++;
                next;
            }

            if ( $state == 1 ) {
                if ( $line =~ /^x NO /i ) {
                    print "ERROR: Authentication failed\n";
                    return;
                }
                elsif ( $line =~ /^x OK /i ) {
                    $imap->writeln( qq{x EXAMINE "$m"} );
                    $state++;
                    next;
                }
            }

            if ( $state == 2 ) {
                if ( $line =~ /^x NO /i ) {
                    print "ERROR: Couldn't select mailbox '$m'\n";
                    return;
                }
                elsif ( $line =~ /^\* / ) {
                    if ( $line =~ /^\* OK \[UIDVALIDITY (\d+)\]$/ ) {
                        $uidvalidity = $1;
                        $dir = "gmail/$l/$uidvalidity";
                        mkdir $dir unless -d $dir;
                        mkdir "$dir/new" unless -d "$dir/new";
                        mkdir "$dir/tmp" unless -d "$dir/tmp";
                    }
                }
                elsif ( $line =~ /^x OK /i ) {
                    $imap->writeln( qq{x FETCH 1:* UID} );
                    $state++;
                    next;
                }
            }

            # We receive the MSN->UID map and check which UIDs we have
            # already. If it's in tmp, it wasn't fully downloaded, so we
            # construct a partial FETCH for it; otherwise we add it to a
            # set of messages for which we send one consolidated FETCH.

            if ( $state == 3 ) {
                if ( $line =~ /^x NO /i ) {
                    print "ERROR: Couldn't fetch UID map\n";
                    return;
                }
                elsif ( $line =~ /^\* (\d+) FETCH \(UID (\d+)\)$/ ) {
                    my ( $msn, $uid ) = ( $1, $2 );
                    if ( -f "$dir/tmp/$uid" ) {
                        my $n = -s "$dir/tmp/$uid";
                        push @tmps,
                            qq{x FETCH $msn (UID BODY[]<$n.2147483648>)};
                    }
                    elsif ( ! -f "$dir/new/$uid" ) {
                        push @msns, $msn;
                    }
                }
                elsif ( $line =~ /^x OK /i ) {
                    my @set;
                    @msns = sort { $a <=> $b } @msns;
                    while ( @msns ) {
                        my ( $min, $max );

                        $min = $max = shift @msns;
                        while ( $msns[0] == $max+1 ) {
                            $max = shift @msns;
                        }

                        if ( $min == $max ) {
                            push @set, $min;
                        }
                        else {
                            push @set, "$min:$max";
                        }
                    }

                    my $set = join ",", @set;
                    unless ( $set || @tmps ) {
                        print "Mail from $m already downloaded to $dir\n";
                        return;
                    }
                    foreach ( @tmps ) {
                        $imap->writeln( $_ );
                    }
                    if ( $set ) {
                        $imap->writeln( qq{xyz FETCH $set (UID BODY[])} );
                    }
                    $state++;
                    next;
                }
            }

            # Each FETCH response will contain the UID and some literal
            # data from the BODY[]. We append the data to tmp/$uid as we
            # receive it, and rename the file to new/$uid when the whole
            # literal is received. If the last step is skipped due to a
            # network error or interrupt, we will ask for the remaining
            # data next time.

            if ( $state == 4 ) {
                if ( $line =~ /^\* \d+ FETCH \(UID (\d+) BODY\[\](?:<\d+>)? {(\d+)}$/ ) {
                    ( $uid, $literal ) = ($1, $2);
                    $written = 0;
                    $file = "$dir/tmp/$uid";
                    unless ( open ( $fh, ">>", "$dir/tmp/$uid" ) ) {
                        print "ERROR: Can't open $file: $!\n";
                        return;
                    }
        LITERAL:
                    my $data = substr( $buf, 0, $literal-$written, '' );
                    if ( $data ) {
                        unless ( syswrite $fh, $data ) {
                            print "ERROR: Can't write to $file: $!\n";
                            return;
                        }
                        $written += length $data;
                    }
                    last if $written < $literal;

                    print "Saving message $dir/new/$uid (got $literal bytes)\n";
                    unless ( rename( $file, "$dir/new/$uid" ) ) {
                        print "ERROR: Can't rename $file: $!\n";
                        return;
                    }
                    $written = $literal = 0;
                    close $fh;
                }
                elsif ( $line =~ /^xyz OK /i ) {
                    # We cheat and pay attention only to the very last
                    # OK response to the FETCHes we sent.
                    $imap->writeln( qq{pqr LOGOUT} );
                    $state++;
                    next;
                }
            }

            if ( $state == 5 ) {
                $state = "logout" if $line =~ /^pqr OK /i;
            }
        }
    }
}

sub IO::Socket::SSL::writeln {
    my ($s, $line) = @_;
    $s->blocking(1);
    print "C: $line\n" if $main::verbose;
    print $s $line, "\r\n";
    $s->blocking(0);
}
